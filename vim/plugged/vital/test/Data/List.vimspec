Describe Data.List
  Before all
    let List = vital#of('vital').import('Data.List')
  End

  Describe .pop()
    It removes the last element from a list and returns that element
      let a = [1,2,3]
      Assert Equals(3, List.pop(a))
      Assert Equals([1, 2], a)
      Assert Equals(2, List.pop(a))
      Assert Equals([1], a)
      Assert Equals(1, List.pop(a))
      Assert Equals([], a)
      Assert Equals(9, List.pop(range(10)))
    End

    It causes an error when the list is empty
      Throw /^Vim(\w*):E684:/ List.pop([])
    End
  End

  Describe .push()
    It appends an element to a list and returns the list itself
      let a = []
      Assert Equals([1], List.push(a, 1))
      Assert Equals([1], a)
      Assert Equals([1,2], List.push(a, 2))
      Assert Equals([1,2], a)
      Assert Equals([1, 2, 3, 4], List.push([1, 2, 3], 4))
      Assert Equals([7, 8, 9, 10], List.push(range(7, 9), 10))
    End
  End

  Describe .shift()
    It removes the first element from a list and returns that element
      let a = [1,2,3]
      Assert Equals(1, List.shift(a))
      Assert Equals([2,3], a)
      Assert Equals(2, List.shift(a))
      Assert Equals([3], a)
      Assert Equals(3, List.shift(a))
      Assert Equals([], a)
      Assert Equals(0, List.shift(range(10)))
    End

    It causes an error when the list is empty
      Throw /^Vim(\w*):E684:/ List.shift([])
    End
  End

  Describe .unshift()
    It inserts an element to the head of a list and returns the list itself
      let a = []
      Assert Equals([1], List.unshift(a, 1))
      Assert Equals([1], a)
      Assert Equals([2,1], List.unshift(a, 2))
      Assert Equals([2,1], a)
      Assert Equals([4, 1, 2, 3], List.unshift([1, 2, 3], 4))
      Assert Equals([10, 7, 8, 9], List.unshift(range(7, 9), 10))
    End
  End

  Describe .cons()
    It makes new list which first item is {val} and the rest of items are {list}.
      let a = []
      Assert Equals(List.cons(1, a), [1])
      Assert Equals([], a)
      let a = [1]
      Assert Equals(List.cons(2, a), [2, 1])
      Assert Equals([1], a)
      Assert Equals(List.cons(1, [2, 3]), [1, 2, 3])
      Assert Equals(List.cons(1, []), [1])
      Assert Equals(List.cons([1], [2, 3]), [[1], 2, 3])
    End
  End

  Describe .conj()
    It makes new list which last items are {val} and the preceding items remain {list}.
      let a = []
      Assert Equals(List.conj(a, 1), [1])
      Assert Equals([], a)
      let a = [1]
      Assert Equals(List.conj(a, 2), [1, 2])
      Assert Equals([1], a)
      Assert Equals(List.conj([2, 3], 1), [2, 3, 1])
      Assert Equals(List.conj([], 1), [1])
      Assert Equals(List.conj([2, 3], [1]), [2, 3, [1]])
    End
  End

  Describe .uniq()
    It makes a list unique
      let a = ['vim', 'emacs', 'vim', 'vim']
      Assert Equals(['vim', 'emacs'], List.uniq(a))
      Assert Equals(a, ['vim', 'emacs', 'vim', 'vim'])
      Assert Equals([1.23, [1]], List.uniq([1.23, [1], [1], 1.23]))
      Assert Equals([{'a': 0, 'b': 1}], List.uniq([{'a': 0, 'b': 1}, {'b': 1, 'a': 0}]))
    End

    It supports empty strings as well
      Assert Equals(['', 'v', 'vv'], List.uniq(['', '', 'v', 'vv', '', 'vv', 'v']))
    End
  End

  Describe .uniq_by()
    It makes a list unique based on given expression
      Assert Equals(
      \ [
      \   'vim', 'emacs', 'gVim'
      \ ],
      \ List.uniq_by([
      \       'vim', 'Vim', 'VIM', 'emacs', 'Emacs', 'EMACS', 'gVim', 'GVIM'
      \     ],
      \     'tolower(v:val)'
      \   )
      \ )
    End
  End

  Describe .clear()
    It clears the all items of a list
      let list = [1, 2, 3]
      Assert Equals([], List.clear(list))
      Assert Equals(list, [])
    End

    It returns the passed list directly
      let list = [1, 2, 3]
      Assert Same(List.clear(list), list)
      Assert Equals(list, [])
    End

    It has no effects for empty list
      let list = []
      Assert Equals(List.clear(list), [])
      Assert Equals(list, [])
    End
  End

  Describe .max_by()
    It returns a maximum value in the list through the given expr.
      Assert Equals('hehehe', List.max_by(['hoge', 'foo', 'hehehe', 'yahoo'], 'len(v:val)'))
      Assert Equals(-50, List.max_by([20, -50, -15, 30], 'abs(v:val)'))
    End
    It returns 0 if the list is empty.
      Assert Equals(0, List.max_by([], 'v:val'))
    End
  End

  Describe .min_by()
    It returns a minimum value in the list through the given expr.
      Assert Equals('foo', List.min_by(['hoge', 'foo', 'hehehe', 'yahoo'], 'len(v:val)'))
      Assert Equals(-15, List.min_by([20, -50, -15, 30], 'abs(v:val)'))
    End
    It returns 0 if the list is empty.
      Assert Equals(0, List.min_by([], 'v:val'))
    End
  End

  Describe .span()
    It splits a list into two lists. The former is until the given condition doesn't satisfy.
      Assert Equals([[1, 3], [5, 2]], List.span('v:val < 5', [1, 3, 5, 2]))
      Assert Equals([[], [1, 2, 3, 4, 5]], List.span('v:val > 3', [1, 2, 3, 4, 5]))
      Assert Equals([[1, 2], [3, 4, 5]], List.span('v:val < 3', [1, 2, 3, 4, 5]))
    End

    It of course handles list of list.
      Assert Equals([[[1], [2, 3]], [[], [4]]],
            \ List.span('len(v:val) > 0', [[1], [2, 3], [], [4]]))
    End
  End

  Describe .break()
    It splits a list into two lists. The latter is from the given condition satisfies.
      Assert Equals([[1, 3], [5, 2]], List.break('v:val == 5', [1, 3, 5, 2]))
      Assert Equals([[1, 2, 3], [4, 5]], List.break('v:val > 3', [1, 2, 3, 4, 5]))
      Assert Equals([[], [1, 2, 3, 4, 5]], List.break('v:val < 3', [1, 2, 3, 4, 5]))
    End

    It of course handles list of list.
      Assert Equals([[[1], [2, 3]], [[], [4]]],
            \ List.break('len(v:val) == 0', [[1], [2, 3], [], [4]]))
    End
  End

  Describe .take_while()
    It creates a list from another one, it inspects the original list and takes from its elements to the moment when the condition fails, then it stops processing
      Assert Equals([1, 3], List.take_while('v:val < 5', [1, 3, 5, 2]))
      Assert Equals([], List.take_while('v:val > 3', [1, 2, 3, 4, 5]))
      Assert Equals([1, 2], List.take_while('v:val < 3', [1, 2, 3, 4, 5]))
    End

    It of course handles list of list.
      Assert Equals([[1], [2, 3]],
            \ List.take_while('len(v:val) > 0', [[1], [2, 3], [], [4]]))
    End
  End

  Describe .partition()
    It takes a predicate a list and returns the pair of lists of elements which do and do not satisfy the predicate.
      Assert Equals([[0, 2, 4], [1, 3]], List.partition('v:val % 2 == 0', range(5)))
    End
  End

  Describe .all()
    It returns true if all items in the list fulfill the condition.
      Assert True(List.all('v:val % 2 == 0', [2, 8, 4, 6]))
      Assert False(List.all('v:val % 2 == 0', [2, 8, 5, 6]))
      Assert True(List.all('0 < v:val', [2, 8, 4, 6]))
      Assert False(List.all('0 < v:val', [2, 0, 4, 6]))
    End

    It returns true if the list is empty.
      Assert True(List.all('v:val', []))
    End
  End

  Describe .any()
    It returns true if at least one item in the list fulfills the condition.
      Assert False(List.any('v:val % 2 == 1', [2, 8, 4, 6]))
      Assert True(List.any('v:val % 2 == 1', [2, 8, 5, 6]))
      Assert False(List.any('0 >= v:val', [2, 8, 4, 6]))
      Assert True(List.any('0 >= v:val', [2, 0, 4, 6]))
    End

    It returns false if the list is empty.
      Assert False(List.any('v:val', []))
    End
  End

  Describe .and()
    It returns the conjunction of a boolean list.
      Assert True(List.and([1, 1, 1, 1]))
      Assert False(List.and([1, 0, 1, 1]))
      Assert False(List.and([0, 0, 0, 0]))
    End

    It returns true if the list is empty.
      Assert True(List.and([]))
    End
  End

  Describe .and()
    It returns the disjunction of a boolean list.
      Assert True(List.or([1, 1, 1, 1]))
      Assert True(List.or([1, 0, 1, 1]))
      Assert False(List.or([0, 0, 0, 0]))
    End
    It returns false if the list is empty.
      Assert False(List.or([]))
    End
  End

  Describe .map_accum()
    It is TODO
      Assert Equals([11, 12, 13], List.map_accum('[v:val + v:memo, v:memo]', [1, 2, 3], 10))
      Assert Equals([11, 13, 15], List.map_accum('[v:val + v:memo, v:memo + 1]', [1, 2, 3], 10))
    End
  End

  Describe .foldl()
    It folds a list from left
      Assert Equals(55, List.foldl('v:memo + v:val', 0, range(1, 10)))
      Assert Equals([[[], 1], 2], List.foldl('[v:memo, v:val]', [], [1, 2]))
    End

    It does nothing if the list is empty
      Assert Equals(123, List.foldl('echoerr omg', 123, []))
    End
  End

  Describe .foldl1()
    It folds a list from left
      Assert Equals(55, List.foldl1('v:memo + v:val', range(1, 10)))
      Assert Equals([1, 2], List.foldl1('[v:memo, v:val]', [1, 2]))
    End

    It causes an error when the list is empty
      Throw /^foldl1$/ List.foldl1('123', [])
    End
  End

  Describe .foldr()
    It folds a list from right
      Assert Equals(55, List.foldr('v:memo + v:val', 0, range(1, 10)))
      Assert Equals([[[], 2], 1], List.foldr('[v:memo, v:val]', [], [1, 2]))
    End

    It does nothing if the list is empty
      Assert Equals(123, List.foldr('echoerr omg', 123, []))
    End
  End

  Describe .foldr1()
    It folds a list from left
      Assert Equals(55, List.foldr1('v:memo + v:val', range(1, 10)))
      Assert Equals([2, 1], List.foldr1('[v:memo, v:val]', [1, 2]))
    End

    It causes an error when the list is empty
      Throw /^foldr1$/ List.foldr1('123', [])
    End
  End

  Describe .zip()
    It returns mixed list from arguments
      Assert Equals(List.zip([1,2,3]), [[1],[2],[3]])
      Assert Equals(List.zip([1,2,3],[4,5,6],[7,8,9]), [[1,4,7],[2,5,8],[3,6,9]])
      Assert Equals(List.zip([1,2,3],[4,5],[7,8,9]), [[1,4,7],[2,5,8]])
    End
  End

  Describe .with_index()
    It returns list with index
      Assert Equals(List.with_index(['a', 'b', 'c']), [['a', 0], ['b', 1], ['c', 2]])
      Assert Equals(List.with_index(['a', 'b', 'c'], 3), [['a', 3], ['b', 4], ['c', 5]])
    End
  End

  Describe .flatten()
    It returns list flatten
      Assert Equals(List.flatten(['a', ['b'], 'c']), ['a', 'b', 'c'])
      Assert Equals(List.flatten(['a', [['b'], 'c']]), ['a', 'b', 'c'])
      Assert Equals(List.flatten([['a', ['b']], 'c']), ['a', 'b', 'c'])
      Assert Equals(List.flatten(['a', [[['b']], 'c']]), ['a', 'b', 'c'])
      Assert Equals(List.flatten(['a', [[['b']], 'c']], 1), ['a', [['b']], 'c'])
      Assert Equals(List.flatten([[['a']], [[['b']], 'c']], 1), [['a'], [['b']], 'c'])
      Assert Equals(List.flatten([[['a']], [[['b']], 'c']], 2), ['a', ['b'], 'c'])
      Assert Equals(List.flatten([[['a']], [[['b']], 'c']], 3), ['a', 'b', 'c'])
      Assert Equals(List.flatten([[['a']], [[['b']], 'c']], 4), ['a', 'b', 'c'])
      Assert Equals(List.flatten([[['a']], [[['b']], 'c']], 10), ['a', 'b', 'c'])
    End
  End

  Describe .find()
    It returns the value if found
      Assert Equals(List.find([1, 2, 3], '*not-found*', 'v:val % 2 == 0'), 2)
      Assert Equals(List.find([1, 2, 3, 1, 2, 3], '*not-found*', 'v:val % 2 == 0'), 2)
      Assert Equals(List.find([1, 2, 3], '*not-found*', 'v:val % 10 == 0'), '*not-found*')
      Assert Equals(List.find([], '*not-found*', 'v:val % 2 == 0'), '*not-found*')

      Assert Equals(List.find(['hi', 'yo'], -1, 'v:val =~ "y"'), 'yo')
    End
  End

  Describe .find_index()
    It returns the index if found
      Assert Equals(List.find_index([1, 2, 3], 'v:val % 2 == 0'), 1)
      Assert Equals(List.find_index(["hoge", "fuga", "piyo"], 'v:val =~# "o"'), 0)
      Assert Equals(List.find_index([1, 2, 3], 'v:val % 2 == 1', 1), 2)
      Assert Equals(List.find_index(["hoge", "fuga", "piyo"], 'v:val =~# "o"', -2), 2)
    End
    It returns -1 or default value if not found
      Assert Equals(List.find_index([], '1'), -1)
      Assert Equals(List.find_index(["hoge", "fuga", "piyo"], 'v:val =~# "x"'), -1)
      Assert Equals(List.find_index([], '1', 0, 999), 999)
      Assert Equals(List.find_index(["hoge", "fuga", "piyo"], 'v:val =~# "x"', 0, "not found"), "not found")
    End
  End

  Describe .find_last_index()
    It returns the index if found
      Assert Equals(List.find_last_index([1, 2, 3, 4], 'v:val % 2 == 0'), 3)
      Assert Equals(List.find_last_index(["hoge", "fuga", "piyo"], 'v:val =~# "o"'), 2)
      Assert Equals(List.find_last_index([1, 2, 3, 4], 'v:val % 2 == 0', 1), 1)
      Assert Equals(List.find_last_index(["hoge", "fuga", "piyo"], 'v:val =~# "o"', -2), 0)
    End
    It returns -1 or default value if not found
      Assert Equals(List.find_last_index([], '1'), -1)
      Assert Equals(List.find_last_index(["hoge", "fuga", "piyo"], 'v:val =~# "x"'), -1)
      Assert Equals(List.find_last_index([], '1', 0, 999), 999)
      Assert Equals(List.find_last_index(["hoge", "fuga", "piyo"], 'v:val =~# "x"', 0, "not found"), "not found")
    End
  End

  Describe .find_indices()
    It returns index list if found
      Assert Equals(List.find_indices([1, 2, 3], 'v:val % 2 == 1'), [0, 2])
      Assert Equals(List.find_indices(["hoge", "fuga", "piyo"], 'v:val =~# "o"'), [0, 2])
      Assert Equals(List.find_indices([1, 2, 3], 'v:val % 2 == 1', 1), [2])
      Assert Equals(List.find_indices([], '1'), [])
      Assert Equals(List.find_indices(["hoge", "fuga", "piyo"], 'v:val =~# "x"'), [])
    End
  End

  Describe .has_common_items()
    It returns true/false if given Lists does/doesn't have any common item(s)
      Assert True(List.has_common_items(['a', 'b', 'c'], ['b', 'c']))
      Assert True(List.has_common_items(['a', 'c'], ['b', 'c']))
      Assert False(List.has_common_items(['a'], ['b', 'c']))
      Assert False(List.has_common_items([], ['b', 'c']))
      Assert False(List.has_common_items(['a'], []))
      Assert False(List.has_common_items([], []))
    End
  End

  Describe .intersect()
    It returns elements which are common with two lists
      Assert Equals(List.intersect(['a', 'b', 'c'], ['b', 'c']), ['b', 'c'])
      Assert Equals(List.intersect(['a', 'c'], ['b', 'c']), ['c'])
      Assert Equals(List.intersect(['a'], ['b', 'c']), [])
      Assert Equals(List.intersect([], ['b', 'c']), [])
      Assert Equals(List.intersect(['a'], []), [])
      Assert Equals(List.intersect([], []), [])
      Assert Equals(List.intersect(['a', 'a'], ['a', 'a']), ['a'])
      Assert Equals(List.intersect([function('tr')], [function('tr')]), [function('tr')])
    End
  End

  Describe .group_by()
    It returns grouped dictionary
      Assert Equals(List.group_by([], 'len(v:val)'), {})
      Assert Equals(List.group_by(['a', 'b', 'ab', 'bc'], 'len(v:val)'), {'1': ['a', 'b'], '2': ['ab', 'bc']})
      Assert Equals(List.group_by(['a', 'b', 'ab', 'bc'], 'v:val[0]'), {'a': ['a', 'ab'], 'b': ['b', 'bc']})
      Assert Equals(List.group_by(range(4), 'v:val % 2 == 0 ? "aa" : 1.0'), {'aa': [0, 2], '1.0': [1, 3]})
    End
  End

  function! My_predicate(a, b) abort
      return a:a - a:b
  endfunction

  Describe .binary_search()
    It search a list and return the index if success
      Assert Equals(List.binary_search([1, 3, 5, 7], 1), 0)
      Assert Equals(List.binary_search([1, 3, 5, 7], 3), 1)
      Assert Equals(List.binary_search([1, 3, 5, 7], 5), 2)
      Assert Equals(List.binary_search([1, 3, 5, 7], 7), 3)

      Assert Equals(List.binary_search([1, 3, 5, 7], 1, 'My_predicate'), 0)
      Assert Equals(List.binary_search([1, 3, 5, 7], 3, 'My_predicate'), 1)
      Assert Equals(List.binary_search([1, 3, 5, 7], 5, 'My_predicate'), 2)
      Assert Equals(List.binary_search([1, 3, 5, 7], 7, 'My_predicate'), 3)

      let f = {}
      function! f.func(a, b) dict abort
        return a:a - a:b
      endfunction

      Assert Equals(List.binary_search([1, 3, 5, 7], 1, f.func, f), 0)
      Assert Equals(List.binary_search([1, 3, 5, 7], 3, f.func, f), 1)
      Assert Equals(List.binary_search([1, 3, 5, 7], 5, f.func, f), 2)
      Assert Equals(List.binary_search([1, 3, 5, 7], 7, f.func, f), 3)

      Assert Equals(List.binary_search([1, 3, 5, 7], 1, f.func), 0)
      Assert Equals(List.binary_search([1, 3, 5, 7], 3, f.func), 1)
      Assert Equals(List.binary_search([1, 3, 5, 7], 5, f.func), 2)
      Assert Equals(List.binary_search([1, 3, 5, 7], 7, f.func), 3)

      function! f.bylength(a, b) dict abort
        return len(a:a) - len(a:b)
      endfunction

      Assert Equals(List.binary_search(['a', 'aa', 'aaa', 'aaaa'], 'a', f.bylength), 0)
      Assert Equals(List.binary_search(['a', 'aa', 'aaa', 'aaaa'], 'aa', f.bylength), 1)
      Assert Equals(List.binary_search(['a', 'aa', 'aaa', 'aaaa'], 'aaa', f.bylength), 2)
      Assert Equals(List.binary_search(['a', 'aa', 'aaa', 'aaaa'], 'aaaa', f.bylength), 3)
      Assert Equals(List.binary_search(['a', 'aa', 'aaa', 'aaaa'], 'aaaaa', f.bylength), -1)

      " Corner cases
      Assert Equals(List.binary_search([1], 1), 0)
    End

    It returns -1 if failed
      Assert Equals(List.binary_search([1, 3, 5, 7], 2), -1)
      Assert Equals(List.binary_search([1, 3, 5, 7], 2, 'My_predicate'), -1)
      Assert Equals(List.binary_search([], 1), -1)
      Assert Equals(List.binary_search([], 1, 'My_predicate'), -1)

      let f = {}
      function! f.func(a, b) dict abort
          return a:a - a:b
      endfunction

      Assert Equals(List.binary_search([1, 3, 5, 7], 2, f.func, f), -1)
      Assert Equals(List.binary_search([], 1, f.func, f), -1)
      Assert Equals(List.binary_search([1, 3, 5, 7], 2, f.func), -1)
      Assert Equals(List.binary_search([], 1, f.func), -1)
    End
  End

  Describe .product()
    It returns Cartesian product of elements in given lists
      Assert Equals(List.product([[1], [2]]), ([[1, 2]]))
      Assert Equals(List.product([[1, 2], [4, 5]]), ([[1, 4], [1, 5], [2, 4], [2, 5]]))
      Assert Equals(List.product([range(2), range(2), range(2)]), ([[0, 0, 0], [0, 0, 1], [0, 1, 0], [0, 1, 1], [1, 0, 0], [1, 0, 1], [1, 1, 0], [1, 1, 1]]))
    End
  End

  Describe .permutations()
    It returns permutations of elements in given list
      Assert Equals(List.permutations([1, 2, 3]), [[1, 2, 3], [1, 3, 2], [2, 1, 3], [2, 3, 1], [3, 1, 2], [3, 2, 1]])
      Assert Equals(List.permutations([]), [[]])
    End

    It returns r length permutations of elements in given list if r is given
      Assert Equals(List.permutations([1, 2, 3], 1), [[1], [2], [3]])
      Assert Equals(List.permutations([1, 2, 3], 2), [[1, 2], [1, 3], [2, 1], [2, 3], [3, 1], [3, 2]])
      Assert Equals(List.permutations([1, 2, 3], 4), [])
    End
  End

  Describe .combinations()
    It returns combinations of elements in given list
      Assert Equals(List.combinations([1, 2, 3, 4], 2), [[1, 2], [1, 3], [1, 4], [2, 3], [2, 4], [3, 4]])
      Assert Equals(List.combinations([5, 2, 3, 1], 3), [[5, 2, 3], [5, 2, 1], [5, 3, 1], [2, 3, 1]])
      Assert Equals(List.combinations([1, 2, 3], 4), [])
    End
  End

End

